@prefix rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#> .
@prefix owl:   <http://www.w3.org/2002/07/owl#> .
@prefix vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#> .
@prefix dynapi: <https://vivoweb.org/ontology/vitro-dynamic-api#> .
@prefix xsd: <http://www.w3.org/2001/XMLSchema#> .
@prefix : <https://vivoweb.org/ontology/vitro-dynamic-api/gesah/export_cultural_object_to_lido/> .
@prefix paramType: <https://vivoweb.org/ontology/vitro-dynamic-api/parameter/type/> .

:operation_export_cultural_object_to_lido dynapi:sparqlQueryText """
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX gesah: <http://ontology.tib.eu/gesah/> 
CONSTRUCT {
  ?resource_id rdf:type ?type .
  ?resource_id rdfs:comment "target_object" .
  ?resource_id xsd:date ?current_date .
  ?resource_id rdfs:label ?co_label .
  ?resource_id vitro:mostSpecificType ?most_specific_type .
  ?resource_id gesah:has_Title ?title_ind .
  ?title_ind gesah:has_title_type ?title_type .
  ?title_type rdfs:label ?title_type_label .
  ?title_type gesah:AAT_ID ?aat_id .
  ?title_ind gesah:title ?title_label .
  ?most_specific_type rdfs:label ?most_specific_type_label .
  ?most_specific_type rdfs:label ?most_specific_type_label .
  ?resource_id gesah:has_object_category_generic_term ?object_category_generic_term .
  ?object_category_generic_term rdfs:label ?object_category_generic_term_label .
  ?resource_id gesah:has_object_category_narrower_term ?object_category_narrower_term .
  ?object_category_narrower_term rdfs:label ?object_category_narrower_term_label .
  ?resource_id gesah:object_of_inscription ?object_of_inscription .

  ?object_of_inscription rdfs:label ?object_of_inscription_label .
  ?object_of_inscription gesah:has_inscription_output ?inscription_output . 
  ?inscription_output gesah:transcription ?inscription_transcription .
  ?inscription_output gesah:has_inscription_type ?inscription_type .
  ?inscription_type rdfs:label ?inscription_type_label .
  ?inscription_output gesah:comment ?output_comment .
  ?inscription_output rdfs:comment ?inscription_output_most_specific_type_literal .

  ?resource_id gesah:current_inventory_number ?current_inv_number .
  ?resource_id gesah:former_inventory_number ?former_inv_number .
  ?resource_id gesah:has_current_holding_institution ?current_holding_institution .
  ?current_holding_institution rdfs:label ?current_holding_institution_label .
  ?resource_id gesah:has_digital_representation ?representation .
  ?representation ?representation_p ?representation_o .
  ?representation gesah:digital_image_right_label ?digital_image_right_label .
  ?resource_id gesah:has_measurements ?measurement .
  ?measurement gesah:measurement_height ?measurement_height .
  ?measurement gesah:measurement_width ?measurement_width .
  ?measurement gesah:measurement_diameter ?measurement_diameter .
  ?measurement gesah:measurement_depth ?measurement_depth .
  ?measurement gesah:has_measurement_specification ?measurement_specification .
  ?measurement_specification rdfs:label ?measurement_specification_label .
  
  ?resource_id ?activity_property ?activity .
  ?activity vitro:mostSpecificType ?activity_type .
  ?activity_type rdfs:label ?activity_type_label .
  ?activity gesah:has_material ?material .
  ?material rdfs:label ?label .
  ?material gesah:AAT_ID ?material_aat_id .
  ?activity gesah:uses_technique ?technique .
  ?technique rdfs:label ?label .
  ?technique gesah:AAT_ID ?technique_aat_id .
  ?activity gesah:has_participant ?activity_participant . 
  ?activity_participant rdfs:label ?activity_participant_type .
  ?activity_participant rdfs:label ?activity_participant_label .
  ?activity gesah:has_type_of_attribution ?activity_type_of_attribution . 
  ?activity_type_of_attribution rdfs:label ?activity_type_of_attribution_label .

  ?activity gesah:has_place ?activity_place . 
  ?activity_place rdfs:label ?activity_place_label .
  ?activity_place gesah:TGN_ID ?activity_place_TGN_ID .
  ?activity_place gesah:GeoNames_ID ?activity_place_GeoNames_ID .
 
  ?resource_id gesah:description ?description .
  ?resource_id gesah:condition_state ?condition_state .
}                
WHERE {
  ?resource_id rdf:type ?type .
  BIND( now() AS ?current_date ) .
  {
    ?resource_id vitro:mostSpecificType ?most_specific_type .
    ?most_specific_type rdfs:label ?most_specific_type_label .
  } UNION
  {
    ?resource_id rdfs:label ?co_label .
  } UNION
  {
    ?resource_id gesah:has_Title ?title_ind .
    {
      ?title_ind gesah:title ?title_label .
    } UNION 
    {
      ?title_ind gesah:has_title_type ?title_type .
      ?title_type rdfs:label ?title_type_label .
      ?title_type gesah:AAT_ID ?aat_id .
    } 
  } UNION
  {
    ?resource_id gesah:has_object_category_generic_term ?object_category_generic_term .
    ?object_category_generic_term rdfs:label ?object_category_generic_term_label .
  } UNION
  {
    ?resource_id gesah:has_object_category_narrower_term ?object_category_narrower_term .
    ?object_category_narrower_term rdfs:label ?object_category_narrower_term_label .
  } UNION
  {
    ?resource_id gesah:object_of_inscription ?object_of_inscription .
    { 
      ?object_of_inscription rdfs:label ?object_of_inscription_label . 
    }
    UNION
    { 
      ?object_of_inscription gesah:has_inscription_output ?inscription_output . 
      {
        ?inscription_output gesah:transcription ?inscription_transcription .
      } 
      UNION
      {
        ?inscription_output gesah:has_inscription_type ?inscription_type .
        ?inscription_type rdfs:label ?inscription_type_label .
      } 
      UNION
      {
        ?inscription_output gesah:comment ?output_comment .
      }
      UNION
      {
        ?inscription_output vitro:mostSpecificType ?inscription_output_most_specific_type .
        BIND (str(?inscription_output_most_specific_type) as ?inscription_output_most_specific_type_literal) 
      }  
    }
    
  } UNION
  {
    ?resource_id gesah:current_inventory_number ?current_inv_number .
  } UNION
  {
    ?resource_id gesah:former_inventory_number ?former_inv_number .
  } UNION
  {
    ?resource_id gesah:has_current_holding_institution ?current_holding_institution .
    ?current_holding_institution rdfs:label ?current_holding_institution_label .
  } UNION {
    ?resource_id gesah:has_digital_representation ?representation .
    {
      ?representation ?representation_p ?representation_o .
    } UNION
    {
      ?representation gesah:digital_image_right ?digital_image_right .
      ?digital_image_right rdfs:label ?digital_image_right_label .
    } 
  } UNION {
    ?resource_id gesah:has_measurements ?measurement .
    {
      ?measurement gesah:measurement_height ?measurement_height .
    } UNION 
    {
      ?measurement gesah:measurement_width ?measurement_width .
    }  UNION 
    {
      ?measurement gesah:measurement_diameter ?measurement_diameter .
    } UNION 
    {
      ?measurement gesah:measurement_depth ?measurement_depth .
    } UNION 
    {
      ?measurement gesah:has_measurement_specification ?measurement_specification .
      ?measurement_specification rdfs:label ?measurement_specification_label .
    }
  } 
  UNION
  {
    ?resource_id ?activity_property ?activity .
    
    { ?activity_property rdfs:subPropertyOf gesah:output_of_production  . }
      UNION
    { ?activity_property rdfs:subPropertyOf gesah:output_of_creation   . }
      UNION
    { ?activity_property rdfs:subPropertyOf gesah:object_of_edition  . }

    {
      ?activity vitro:mostSpecificType ?activity_type .
      ?activity_type rdfs:label ?activity_type_label .
    }
      UNION
    {
      ?activity gesah:has_material ?material .
      {
        ?material rdfs:label ?label .
      }
        UNION
      {
        ?material gesah:AAT_ID ?material_aat_id .
      }
    }
      UNION
    {
      ?activity gesah:uses_technique ?technique .
      {
        ?technique rdfs:label ?label .
      }
        UNION
      {
        ?technique gesah:AAT_ID ?technique_aat_id .
      }
    }  
      UNION 
    {
      ?activity gesah:has_participant ?activity_participant . 
      ?activity_participant rdfs:label ?activity_participant_type .
      ?activity_participant rdfs:label ?activity_participant_label .
    }
      UNION 
    {
      ?activity gesah:has_type_of_attribution ?activity_type_of_attribution . 
      ?activity_type_of_attribution rdfs:label ?activity_type_of_attribution_label .
    }
      UNION 
    {
      ?activity gesah:has_place ?activity_place . 
      {
        ?activity_place rdfs:label ?activity_place_label .
      }
        UNION
      {
        ?activity_place gesah:TGN_ID ?activity_place_TGN_ID .
      }
        UNION
      {
        ?activity_place gesah:GeoNames_ID ?activity_place_GeoNames_ID .
      }
    }
  }
  UNION
  {
    ?resource_id gesah:description ?description_literal .
    BIND ( str(?description_literal) as ?description )
  } 
  UNION
  {
    ?resource_id gesah:condition_state ?condition_state .
  } 
}
""" .
